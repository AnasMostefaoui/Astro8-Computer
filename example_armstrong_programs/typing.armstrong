#AS

////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// This is a very simple typing program. All of the letters and many symbols can be typed with your keyboard. //
// You can also delete previously typed characters with the BACKSPACE key.                                    //
////////////////////////////////////////////////////////////////////////////////////////////////////////////////


change $chMemOffset = 61294
change $charCount = 0
change @EX = 168
change $cursorChar = 8
change $lastKey = 168


// Cursor/clear character
add $chMemOffset,$charCount -> $actualAddress
change *$actualAddress = $cursorChar

#mainLoop // Main loop

// Get any key press
change $key = @EX

// If the user is pressing a key
if $key!=168:
	// Only get 1 key press if the user holds it down
	if $key!=$lastKey:

		if $key==70: // If backspace key pressed
			if $charCount>0:
				// Clear character
				add $chMemOffset,$charCount -> $actualAddress
				change *$actualAddress = 0

				// Move backward in memory
				sub $charCount,1 -> $charCount
				// Cursor character
				add $chMemOffset,$charCount -> $actualAddress
				change *$actualAddress = $cursorChar
			endif
		endif

		if $key!=70: // If backspace key is NOT pressed
			// Put character into memory
			add $chMemOffset,$charCount -> $actualAddress
			change *$actualAddress = $key
			
			// Increment character position
			add $charCount,1 -> $charCount

			// Cursor/clear character
			add $chMemOffset,$charCount -> $actualAddress
			change *$actualAddress = $cursorChar

			// Reset character count if exceeds char limit
			if $charCount==110:
				change $charCount = 0
			endif
		endif

		change $lastKey = $key
	endif
endif

// If the user releases the key, reset lastkey
if $key==168:
		change $lastKey = $key
endif


goto #mainLoop